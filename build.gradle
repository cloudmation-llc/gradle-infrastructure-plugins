buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "pl.allegro.tech.build.axion-release" version "1.11.0"
    id "com.gradle.plugin-publish" version "0.11.0"
    id "java-gradle-plugin"
    id "groovy"
    id "maven-publish"
}

scmVersion {
    localOnly = true

    checks {
        uncommittedChanges = false
    }

    tag {
        prefix = ""
    }
}

project.version = scmVersion.version

group "com.cloudmation"

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation platform("software.amazon.awssdk:bom:2.13.+")
    implementation "software.amazon.awssdk:cloudformation:2.13.+"
    implementation "org.apache.commons:commons-text:1.8"
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/cloudmation-llc/cloudmation-gradle-project-plugins")
            credentials {
                username = project.findProperty("com.cloudmation.ghp.username")
                password = project.findProperty("com.cloudmation.ghp.token")
            }
        }
    }

    publications {
        gradlePlugin(MavenPublication) {
            artifactId = "gradle-project-plugins"
            from components.java

            versionMapping {
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}

gradlePlugin {
    plugins {
        awsProjectSettings {
            id = "com.cloudmation.aws-project-settings"
            displayName = "AWS Project Settings"
            description = "A settings plugin that provides a directory convention and subproject configuration for AWS projects"
            implementationClass = "com.cloudmation.gradle.aws.AwsProjectSettingsPlugin"
        }

        awsProject {
            id = "com.cloudmation.aws-project"
            displayName = "AWS Project"
            description = "A plugin that scans for AWS specific projects and dynamically generates Gradle tasks"
            implementationClass = "com.cloudmation.gradle.aws.AwsProjectPlugin"
        }
    }
}

pluginBundle {
    website = "https://github.com/cloudmation-llc"
    vcsUrl = "https://github.com/cloudmation-llc/cloudmation-gradle-project-plugins"
    tags = ["aws", "cloudformation"]
}